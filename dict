#!/usr/bin/env perl
# martin, 2018-12-10, 2019-01-12, 2019-06-30, 2020-12-20
# ... ask dict.cc for a translation

use strict;
use warnings;
use v5;

use Encode;

use Mojo::UserAgent;
use URI::Escape;

use open qw(:std :utf8);

sub fetch_list {
  my ($htmlref, $id) = @_;

  return
    map  { decode('UTF-8', $_) }
    grep { $_ ne '' }
    map  { s/\\'/'/g; $_ }
    map  { s/(^"|"$)//g; $_ }
    map  { /\((.*?)\);/; split /","/, $1 }
    grep { /var c${id}Arr/ }
    split /\n/, $$htmlref;
}

sub lookup {
  my $term = join(' ', @_);
  my $url = 'https://www.dict.cc/?s=' . uri_escape($term);

  my $res = Mojo::UserAgent->new->get($url)->res;

  if ($res->code != 200) {
    die "dict.cc returned: " . $res->code . "\n";
  }

  my $html = $res->body;

  my @xs1 = fetch_list(\$html, 1);
  my @xs2 = fetch_list(\$html, 2);

  for (0..$#xs1) {
    printf "%s -- %s\n", $xs1[$_], $xs2[$_];
  }
}

sub show_help {
  print <<'EOF';
Usage: dict [-h | words...]
Look up a German/English translation in dict.cc

Example: dict talk to the hand

This file is part of Martin's scripts.
<https://github.com/nanont/scripts>

Licensed under MIT.
EOF
}

sub main {
  if (!(scalar @ARGV) or $ARGV[0] =~ /--?h(elp)?/) {
    show_help;
    exit;
  }

  lookup(@ARGV);
}

main;
